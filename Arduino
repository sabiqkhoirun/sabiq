#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include "DHT.h"
#include <Wire.h>
#include <LiquidCrystal_I2C.h>

// ====== KONFIGURASI ======
const char* ssid = "sabiq";
const char* wifi_pass = "sabiq123";

const char* mqtt_server = "x2.revolusi-it.com";
const uint16_t mqtt_port = 1883;
const char* mqtt_user = "usm";
const char* mqtt_pass = "usmjaya25";

const char* NIM_MCU = "G.231.23.0036";

char mqtt_topic[80];
char mqtt_topic_led[80];
char mqtt_topic_alert[80];

// DHT pada D7 (pastikan wiring sensor ke D7)
#define DHTPIN D7
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);

// LCD I2C
LiquidCrystal_I2C lcd(0x27, 16, 2);

// LED utama (ON/OFF) di D6
const uint8_t LED_PIN = D6;
bool ledState = false;

// LED alarm berkedip di D5 saat suhu > 30Â°C
const uint8_t ALERT_PIN = D5;
bool alertActive = false;
bool alertLedState = false;
unsigned long lastBlink = 0;
const unsigned long blinkInterval = 500; // ms

// WiFi & MQTT
WiFiClient espClient;
PubSubClient client(espClient);

unsigned long lastMsg = 0;
const unsigned long publishInterval = 3000; // ms

void setup_wifi() {
  delay(10);
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, wifi_pass);
  Serial.print("Connecting WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println();
  Serial.print("IP: ");
  Serial.println(WiFi.localIP());
}

void publishLedState(bool state) {
  char buf[40];
  snprintf(buf, sizeof(buf), "{\"led\":%d}", state ? 1 : 0);
  client.publish(mqtt_topic_led, buf, true);
}

void publishAlertState(bool state) {
  char buf[40];
  snprintf(buf, sizeof(buf), "{\"alert\":%d}", state ? 1 : 0);
  client.publish(mqtt_topic_alert, buf, true);
}

void callback(char* topic, byte* payload, unsigned int length) {
  String msg;
  for (unsigned int i = 0; i < length; i++) msg += (char)payload[i];

  if (String(topic) == String(mqtt_topic_led)) {
    if (msg.equalsIgnoreCase("ON")) {
      ledState = true;
      digitalWrite(LED_PIN, HIGH);
    } else if (msg.equalsIgnoreCase("OFF")) {
      ledState = false;
      digitalWrite(LED_PIN, LOW);
    } else if (msg.equalsIgnoreCase("TOGGLE")) {
      ledState = !ledState;
      digitalWrite(LED_PIN, ledState ? HIGH : LOW);
    } else if (msg.indexOf("\"led\"") >= 0) {
      if (msg.indexOf(":1") >= 0) {
        ledState = true;
        digitalWrite(LED_PIN, HIGH);
      } else {
        ledState = false;
        digitalWrite(LED_PIN, LOW);
      }
    }
    publishLedState(ledState);
  }
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("MQTT connect...");
    String clientId = String(NIM_MCU);
    if (client.connect(clientId.c_str(), mqtt_user, mqtt_pass)) {
      Serial.println("connected");
      client.setCallback(callback);
      client.subscribe(mqtt_topic_led);
      // kirim status awal
      publishLedState(ledState);
      publishAlertState(alertActive);
    } else {
      Serial.print("failed rc=");
      Serial.print(client.state());
      Serial.println(" try again in 2s");
      delay(2000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  dht.begin();

  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, LOW);
  ledState = false;

  pinMode(ALERT_PIN, OUTPUT);
  digitalWrite(ALERT_PIN, LOW);
  alertActive = false;
  alertLedState = false;

  Wire.begin(D2, D1); // SDA = D2, SCL = D1
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.setCursor(0,0);
  lcd.print("IoT Starting...");

  snprintf(mqtt_topic, sizeof(mqtt_topic), "iot/%s", NIM_MCU);
  snprintf(mqtt_topic_led, sizeof(mqtt_topic_led), "iot/%s/led", NIM_MCU);
  snprintf(mqtt_topic_alert, sizeof(mqtt_topic_alert), "iot/%s/alert", NIM_MCU);

  setup_wifi();
  client.setServer(mqtt_server, mqtt_port);

  delay(500);
  lcd.clear();
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();

  unsigned long now = millis();

  // non-blocking blink untuk ALERT_PIN (D5)
  if (alertActive) {
    if (now - lastBlink >= blinkInterval) {
      lastBlink = now;
      alertLedState = !alertLedState;
      digitalWrite(ALERT_PIN, alertLedState ? HIGH : LOW);
    }
  } else {
    if (alertLedState) {
      alertLedState = false;
      digitalWrite(ALERT_PIN, LOW);
    }
  }

  if (now - lastMsg > publishInterval) {
    lastMsg = now;

    float h = dht.readHumidity();
    float t = dht.readTemperature();

    // Serial.print("Baca Sensor... "); 
    // -----------------------------------------------

    if (isnan(h) || isnan(t)) {
      Serial.println("GAGAL! (DHT Error)"); 
      lcd.setCursor(0,0);
      lcd.print("DHT Error       ");
      return; 
    }
    
    // Serial.println("OK"); 
    // -----------------------------------------

    // tampilkan ke LCD
    lcd.setCursor(0,0);
    lcd.print("Suhu:");
    lcd.print(t,1);
    lcd.print("C   ");
    lcd.setCursor(0,1);
    lcd.print("Lemb:");
    lcd.print(h,1);
    lcd.print("%   ");

    // publish sensor
    char payload[128];
    snprintf(payload, sizeof(payload), "{\"nim\":\"%s\",\"t\":%.1f,\"h\":%.1f}", NIM_MCU, t, h);
    
    // Tampilkan data JSON yang dikirim saja (agar tahu data valid)
    Serial.print("Mengirim ke MQTT: ");
    Serial.println(payload);

    if (client.publish(mqtt_topic, payload)) {
       Serial.println("Status: Terkirim!");
    } else {
       Serial.println("Status: Gagal Kirim");
    }

    // aturan otomatis lampu utama
    bool autoOn = (t > 31.0) || (h >= 70.0);
    if (autoOn != ledState) {
      ledState = autoOn;
      digitalWrite(LED_PIN, ledState ? HIGH : LOW);
      publishLedState(ledState);
    }

    // alert untuk kedip
    bool newAlert = (t > 30.0);
    if (newAlert != alertActive) {
      alertActive = newAlert;
      publishAlertState(alertActive);
      lastBlink = now;
      alertLedState = false;
      digitalWrite(ALERT_PIN, LOW);
    }
  }
}
